plugins {
    id "java"
    id "idea"
    id "application"
    id "org.jetbrains.kotlin.jvm" version "1.4.21"
    id "org.jlleitschuh.gradle.ktlint" version "9.4.1"
    id "org.jlleitschuh.gradle.ktlint-idea" version "9.4.1"
    id "org.jetbrains.kotlin.plugin.spring" version "1.4.21"
    id "org.springframework.boot" version "2.4.1"
    id "io.spring.dependency-management" version "1.0.10.RELEASE"
    id "org.openjfx.javafxplugin" version "0.0.9"
    id "org.beryx.runtime" version "1.12.1"
}

repositories {
    maven { url "https://oss.jfrog.org/artifactory/oss-release-local" }
    maven { url "https://oss.jfrog.org/artifactory/oss-snapshot-local" }
    mavenCentral()
}

javafx {
    version = "15.0.1"
    modules = [
        "javafx.controls",
        "javafx.media",
        "javafx.fxml"
    ]
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-javafx:1.1.1"
    implementation "io.github.dylmeadows:spring-boot-starter-javafx:2.4.1-3"
    implementation "org.springframework.boot:spring-boot-starter-webflux"
    implementation "io.github.dylmeadows:commonkt-javafx:0.0.1"

    implementation "com.google.guava:guava:27.1-jre"
    implementation "com.google.code.gson:gson:2.8.5"
    implementation "de.jensd:fontawesomefx:8.9"

    testImplementation "junit:junit:4.12"
    testImplementation "org.testfx:testfx-core:$testfxVersion"
    testImplementation "org.testfx:testfx-junit:$testfxVersion"
}

application {
    mainClassName = "io.github.dylmeadows.eontimer.MainKt"
    applicationName = "EonTimer"
}

compileKotlin {
    kotlinOptions.jvmTarget = "15"
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "15"
}

runtime {
    options = ["--strip-debug", "--compress", "2", "--no-header-files", "--no-man-pages"]
    launcher {
        noConsole = true
    }
    jpackage {
        def currentOs = org.gradle.internal.os.OperatingSystem.current()
        def imgType = currentOs.windows ? "ico" : currentOs.macOsX ? "icns" : "png"
        imageOptions += ["--icon", "src/main/resources/io/github/dylmeadows/eontimer/img/icon/icon.$imgType"]
        installerOptions += ["--resource-dir", "src/main/resources"]
        installerOptions += ["--vendor", "Dylan Meadows"]
        if (currentOs.windows) {
            installerOptions += ["--win-per-user-install", "--win-dir-chooser", "--win-menu", "--win-shortcut"]
        } else if (currentOs.linux) {
            installerOptions += ["--linux-package-name", "EonTimer", "--linux-shortcut"]
        } else if (currentOs.macOsX) {
            installerOptions += ["--mac-package-name", "EonTimer"]
        }
    }
}
